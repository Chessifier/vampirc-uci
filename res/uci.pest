WHITESPACE = _{ " " | "\t" }

commands = _{ SOI ~ ws_nl* ~ message+ ~ !any}

commands_ignore_unknown = _{ SOI ~ ws_nl* ~ (message | something)* }

message = _{ WHITESPACE* ~ (uci | debug | isready | setoption | register | ucinewgame | stop | ponderhit | quit |
            position | go) ~ WHITESPACE* ~ NEWLINE+}

uci = ${ ^"uci" ~ !non_ws }

switch = { ^"on" | ^"off" }
debug = ${ ^"debug" ~ WHITESPACE+ ~ switch }

isready = { ^"isready" }

setoption = ${^"setoption" ~ WHITESPACE+ ~ ^"name" ~ WHITESPACE+ ~ option_internal }
option_internal = ${ option_name ~ (WHITESPACE+ ~ (^"value" ~ WHITESPACE+ ~ option_value))? }
option_name = ${ option_token ~ (WHITESPACE+ ~ option_token)* }
option_token = _{ !^"value" ~ token }
option_value = { any+ }

register = ${ ^"register" ~ WHITESPACE+ ~ (register_later | register_nc) }
register_later = ${ ^"later" ~ !non_ws }
register_nc = ${ ^"name" ~ WHITESPACE+ ~ register_name ~ WHITESPACE+ ~ ^"code" ~ WHITESPACE+ ~ register_code }
register_name = ${ register_token ~ (WHITESPACE+ ~ register_token)* }
register_token = _{ !^"code" ~ token }
register_code = { any+ }

ucinewgame = { ^"ucinewgame" }

stop = ${ ^"stop" ~ !non_ws }

quit = ${ ^"quit" ~ !non_ws }

ponderhit = { ^"ponderhit" }

position = ${ ^"position" ~ WHITESPACE+ ~ (fen_pos | startpos) ~ WHITESPACE* ~ (^"moves" ~WHITESPACE ~ a_move ~
        (WHITESPACE+ ~ a_move)*)* }
square = ${ file ~ rank }
from_sq = { square }
to_sq = { square }
a_move = ${from_sq ~ to_sq ~ promotion? }
promotion = { ^"q" | ^"r" | ^"n" | ^"b" }
startpos = ${ ^"startpos" }
piece_char = { ^"k" | ^"q" | ^"r" | ^"n" | ^"b" | ^"p" }
rank = { '1'..'8' }
file = { ('a'..'h') | ('A'..'H') }

// FEN stuff
fen_pos = _{ ^"fen" ~ WHITESPACE+ ~ fen }
fen = ${ (fen_rank ~ rank_sep){7} ~ fen_rank ~ WHITESPACE+ ~ color ~ WHITESPACE+ ~ castling ~ WHITESPACE+ ~ en_passant
        ~ WHITESPACE+ ~ ply_clock ~ WHITESPACE+ ~ move_num }
rank_sep = _{ "/" }
fen_rank = { (piece_char | rank){1,8} }
color = {^"w" | ^"b"}
fen_none = { "-" }
castling_chars = { ^"k" | ^"q" }
castling = { castling_chars{1,4} | fen_none }
en_passant = { square | fen_none }
counter = _{ digit{1,4} }
ply_clock = { counter }
move_num = { counter }

// GO
go = ${ ^"go" ~ (WHITESPACE+ | go_time | go_search)+ }
go_time = { go_ponder | go_infinite | go_movetime | go_timeleft }
go_ponder = ${ ^"ponder" ~ !non_ws }
go_infinite = ${ ^"infinite" ~ !non_ws }
go_movetime = ${ ^"movetime" ~ WHITESPACE+ ~ milliseconds }
go_timeleft = { (wtime | btime | winc | binc | movestogo)+ }
wtime = ${ ^"wtime" ~ WHITESPACE+ ~ milliseconds}
btime = ${ ^"btime" ~ WHITESPACE+ ~ milliseconds}
winc = ${ ^"winc" ~ WHITESPACE+ ~ milliseconds}
binc = ${ ^"binc" ~ WHITESPACE+ ~ milliseconds}
movestogo = ${ ^"movestogo" ~ WHITESPACE+ ~ digits3}
go_search = { depth | nodes | mate | searchmoves }
depth = ${ ^"depth" ~WHITESPACE+ ~ digits3 }
nodes = ${ ^"nodes" ~WHITESPACE+ ~ digits12 }
mate = ${ ^"mate" ~WHITESPACE+ ~ digits3 }
searchmoves = ${ ^"searchmoves" ~ (WHITESPACE+ ~ a_move)+ }

milliseconds = { digit{1,12} }
digits3 = { digit{1,3} }
digits12 = { digit{1,12} }



token = @{ (!(WHITESPACE | NEWLINE) ~ ANY)+ }
any = _{ !NEWLINE ~ ANY }

alpha = { 'a'..'z' | 'A'..'Z' }
digit = { '0'..'9' }
non_ws = _{!(WHITESPACE | NEWLINE) ~ ANY*}
ws_nl = _{ WHITESPACE | NEWLINE }
something = _{ any+ ~ NEWLINE+}